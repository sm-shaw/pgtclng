#!/bin/bash -norc
dnl	This file is an input file used by the GNU "autoconf" program to
dnl	generate the file "configure", which is run during Tcl installation
dnl	to configure the system for the local environment.
#
# $Id: configure.in 371 2014-09-12 19:37:00Z lbayuk $

#-----------------------------------------------------------------------
# configure.in for pgtcl-ng Tcl Extension.
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# __CHANGE__
# Set your package name and version numbers here.
#
# This initializes the environment with PACKAGE_NAME and PACKAGE_VERSION
# set as provided.  These will also be added as -D defs in your Makefile
# so you can encode the package version directly into the source files.
#-----------------------------------------------------------------------

AC_INIT([pgtcl], [2.1.1])

#--------------------------------------------------------------------
# Call TEA_INIT as the first TEA_ macro to set up initial vars.
# This will define a ${TEA_PLATFORM} variable == "unix" or "windows"
# as well as PKG_LIB_FILE and PKG_STUB_LIB_FILE.
#--------------------------------------------------------------------

TEA_INIT([3.5])

AC_CONFIG_AUX_DIR(tclconfig)

#--------------------------------------------------------------------
# Load the tclConfig.sh file
#--------------------------------------------------------------------

TEA_PATH_TCLCONFIG
TEA_LOAD_TCLCONFIG

#--------------------------------------------------------------------
# Load the tkConfig.sh file if necessary (Tk extension)
#--------------------------------------------------------------------

#TEA_PATH_TKCONFIG
#TEA_LOAD_TKCONFIG

#-----------------------------------------------------------------------
# Handle the --prefix=... option by defaulting to what Tcl gave.
# Must be called after TEA_LOAD_TCLCONFIG and before TEA_SETUP_COMPILER.
#-----------------------------------------------------------------------

TEA_PREFIX

#-----------------------------------------------------------------------
# Standard compiler checks.
# This sets up CC by using the CC env var, or looks for gcc otherwise.
# This also calls AC_PROG_CC, AC_PROG_INSTALL and a few others to create
# the basic setup necessary to compile executables.
#-----------------------------------------------------------------------

TEA_SETUP_COMPILER

#--------------------------------------------------------------------
# __CHANGE__
# Specify the C source files to compile in TEA_ADD_SOURCES,
# public headers that need to be installed in TEA_ADD_HEADERS,
# stub library C source files to compile in TEA_ADD_STUB_SOURCES,
# and runtime Tcl library files in TEA_ADD_TCL_SOURCES.
# This defines PKG(_STUB)_SOURCES, PKG(_STUB)_OBJECTS, PKG_HEADERS
# and PKG_TCL_SOURCES.
#-----------------------------------------------------------------------

TEA_ADD_SOURCES([pgtcl.c pgtclCmds.c pgtclId.c])
TEA_ADD_HEADERS([libpgtcl.h])
TEA_ADD_INCLUDES([])
TEA_ADD_LIBS([])
TEA_ADD_CFLAGS([])
TEA_ADD_STUB_SOURCES([])
TEA_ADD_TCL_SOURCES([])

#--------------------------------------------------------------------
# __CHANGE__
# A few miscellaneous platform-specific items:
#
# Define a special symbol for Windows (BUILD_sample in this case) so
# that we create the export library with the dll.
#
# Windows creates a few extra files that need to be cleaned up.
# You can add more files to clean if your extension creates any extra
# files.
#
# TEA_ADD_* any platform specific compiler/build info here.
#--------------------------------------------------------------------

if test "${TEA_PLATFORM}" = "windows" ; then
    AC_DEFINE(BUILD_sample, 1, [Build windows export dll])
    CLEANFILES="pkgIndex.tcl *.lib *.dll *.exp *.ilk *.pdb vc*.pch"
    #TEA_ADD_SOURCES([win/winFile.c])
    #TEA_ADD_INCLUDES([-I\"$(${CYGPATH} ${srcdir}/win)\"])
else
    CLEANFILES="pkgIndex.tcl"
    #TEA_ADD_SOURCES([unix/unixFile.c])
    #TEA_ADD_LIBS([-lsuperfly])
fi
AC_SUBST(CLEANFILES)

#--------------------------------------------------------------------
# __CHANGE__
# Choose which headers you need.  Extension authors should try very
# hard to only rely on the Tcl public header files.  Internal headers
# contain private data structures and are subject to change without
# notice.
# This MUST be called after TEA_LOAD_TCLCONFIG / TEA_LOAD_TKCONFIG
#--------------------------------------------------------------------

TEA_PUBLIC_TCL_HEADERS
#TEA_PRIVATE_TCL_HEADERS

#TEA_PUBLIC_TK_HEADERS
#TEA_PRIVATE_TK_HEADERS
#TEA_PATH_X

#--------------------------------------------------------------------
# Check whether --enable-threads or --disable-threads was given.
# This auto-enables if Tcl was compiled threaded.
#--------------------------------------------------------------------

#TEA_ENABLE_THREADS

#--------------------------------------------------------------------
# The statement below defines a collection of symbols related to
# building as a shared library instead of a static library.
#--------------------------------------------------------------------

TEA_ENABLE_SHARED

#--------------------------------------------------------------------
# This macro figures out what flags to use with the compiler/linker
# when building shared/static debug/optimized objects.  This information
# can be taken from the tclConfig.sh file, but this figures it all out.
#--------------------------------------------------------------------

TEA_CONFIG_CFLAGS

#--------------------------------------------------------------------
# Set the default compiler switches based on the --enable-symbols option.
#--------------------------------------------------------------------

TEA_ENABLE_SYMBOLS

#--------------------------------------------------------------------
# Everyone should be linking against the Tcl stub library.  If you
# can't for some reason, remove this definition.  If you aren't using
# stubs, you also need to modify the SHLIB_LD_LIBS setting below to
# link against the non-stubbed Tcl library.  Add Tk too if necessary.
#--------------------------------------------------------------------

AC_DEFINE(USE_TCL_STUBS, 1, [Use Tcl stubs])
#AC_DEFINE(USE_TK_STUBS, 1, [Use Tk stubs])

#--------------------------------------------------------------------
# Get the path to the PostgreSQL installation.
#--------------------------------------------------------------------

LIBPG=""
AC_ARG_WITH(postgres-include,[  --with-postgres-include=DIR   PostgreSQL includes are in DIR],
	PG_INC_DIR=$withval, PG_INC_DIR="")
AC_ARG_WITH(postgres-lib,[  --with-postgres-lib=DIR       PostgreSQL libraries are in DIR],
	PG_LIB_DIR=$withval, PG_LIB_DIR="")

# Use pg_config (if found) to supply missing paths, if needed.
if test "$PG_INC_DIR" = "" -o "$PG_LIB_DIR" = ""; then
  AC_PATH_PROG(PG_CONFIG,pg_config)
  if test "$PG_CONFIG" = ""; then
    AC_MSG_ERROR([cannot find pg_config program on PATH. Without pg_config,
you must specify both --with-postgres-include and --with-postgres-lib])
  fi
  if test "$PG_INC_DIR" = ""; then
    PG_INC_DIR=`$PG_CONFIG --includedir`
    AC_MSG_NOTICE([pg_config says includedir is $PG_INC_DIR])
  fi
  if test "$PG_LIB_DIR" = ""; then
    PG_LIB_DIR=`$PG_CONFIG --libdir`
    AC_MSG_NOTICE([pg_config says libdir is $PG_LIB_DIR])
  fi
fi

PG_INCLUDES="-I$PG_INC_DIR"
PG_LIBS="-L$PG_LIB_DIR -lpq"
if test ! "$PG_LIB_DIR" = ""; then
  LIBPG="libpq${SHLIB_SUFFIX}"
fi

AC_SUBST(LIBPG)
AC_SUBST(PG_INCLUDES)
AC_SUBST(PG_LIBS)
AC_SUBST(PG_LIB_DIR)

#--------------------------------------------------------------------
# Function availability tests for the PostgreSQL library:
#--------------------------------------------------------------------
save_LIBS="$LIBS"
LIBS="$PG_LIBS $LIBS"
AC_CHECK_FUNCS(lo_truncate PQencryptPassword)
AC_CHECK_FUNCS(PQescapeStringConn PQescapeByteaConn)
# Note this test which defines HAVE_PQDESCRIBEPREPARED is used for
# PQdescribePrepared, PQdescribePortal, PQnparams, and PQparamtype -
# all of which were added in the same PostgreSQL release 8.2.
AC_CHECK_FUNCS(PQdescribePrepared)
# This test which defines HAVE_LO_TELL64 also covers lo_seek64 and
# lo_truncate64, all of which are added in PostgreSQL 9.3.
AC_CHECK_FUNCS(lo_tell64)
# This test which defines HAVE_PQESCAPELITERAL covers both
# PQescapeliteral and PQescapeIdentifier, both added in PostgreSQL 9.0.
AC_CHECK_FUNCS(PQescapeLiteral)
LIBS="$save_LIBS"

#--------------------------------------------------------------------
# This macro generates a line to use when building a library.  It
# depends on values set by the TEA_ENABLE_SHARED, TEA_ENABLE_SYMBOLS,
# and TEA_LOAD_TCLCONFIG macros above.
#--------------------------------------------------------------------

TEA_MAKE_LIB

#--------------------------------------------------------------------
# Determine the name of the tclsh and/or wish executables in the
# Tcl and Tk build directories or the location they were installed
# into. These paths are used to support running test cases only,
# the Makefile should not be making use of these paths to generate
# a pkgIndex.tcl file or anything else at extension build time.
#--------------------------------------------------------------------

TEA_PROG_TCLSH
#TEA_PROG_WISH

#--------------------------------------------------------------------
# Finally, substitute all of the various values into the Makefile.
# You may alternatively have a special pkgIndex.tcl.in or other files
# which require substituting th AC variables in.  Include these here.
#--------------------------------------------------------------------

# Don't know why this is needed.
AC_SUBST(LD_SEARCH_FLAGS)

AC_CONFIG_FILES([Makefile pkgIndex.tcl])
AC_OUTPUT
